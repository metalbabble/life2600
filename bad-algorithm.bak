;----RULES----
;Any live cell with fewer than two live neighbours dies, as if caused by underpopulation.
;Any live cell with two or three live neighbours lives on to the next generation.
;Any live cell with more than three live neighbours dies, as if by overpopulation.
;Any dead cell with exactly three live neighbours becomes a live cell, as if by reproduction.

 set kernel_options no_blank_lines
 set smartbranching on

 dim chkX = a            
 dim chkY = b
 dim neighbours = c
 dim refreshCounter = d
 dim rand16 = z

 const noscore = 1
 ;scorecolor = $02
 
 const pfres = 12
 const MAXX = 30
 const MAXY = 11
 const REFRESHMAX = 4

bank2
init
 COLUBK = $00
 COLUPF = $22 ;rand
 refreshCounter = 0

seed
 for y = 0 to MAXY
    for x = 0 to MAXX
        COLUPF = rand
        if rand < 20 then pfpixel x y on 
    next
    
    drawscreen
 next

mainLoop
 for y = 0 to MAXY
    for x = 0 to MAXX
        rem -- count neighbours --
        neighbours = 0
        chkX = x - 1
        if pfread(chkX, y) then neighbours = neighbours + 1
        chkY = y - 1
        if pfread(x, chkY) then neighbours = neighbours + 1
        chkX = x + 1
        if pfread(chkX, y) then neighbours = neighbours + 1
        chkY = y + 1
        if pfread(x, chkY) then neighbours = neighbours + 1

        chkX = x - 1 : chkY = y - 1
        if pfread(chkX, chkY) then neighbours = neighbours + 1  
        chkX = x - 1 : chkY = y + 1
        if pfread(chkX, chkY) then neighbours = neighbours + 1  
        chkX = x + 1 : chkY = y - 1
        if pfread(chkX, chkY) then neighbours = neighbours + 1  
        chkX = x + 1 : chkY = y + 1
        if pfread(chkX, chkY) then neighbours = neighbours + 1                  

        rem -- select checks based on dead or living --
        if !pfread(x,y) then goto skipLivingChecks

        rem -- fewer than 2 neighbours dies --
        if neighbours < 2 then pfpixel x y off

        rem -- 2 or 3 lives on --
        ;if neighbours = 2 || neighbours = 3 then pfpixel x y on

        rem -- more than 3 dies --
        if neighbours > 3 then pfpixel x y off
        
        goto finishedChecks

skipLivingChecks
        rem -- dead with 3 turns alive --
        if neighbours = 3 then pfpixel x y on

        
finishedChecks
        if refreshCounter < REFRESHMAX then refreshCounter = refreshCounter + 1 else refreshCounter = 0
        if refreshCounter = 0 then drawscreen
    next
    ;drawscreen
 next
 
 score = score + 1
 goto mainLoop

